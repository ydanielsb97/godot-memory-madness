extends TextureButton

class_name MemoryTile

@onready var frame_image: TextureRect = $FrameImage
@onready var item_image: TextureRect = $ItemImage

func _ready() -> void:
	reveal(false)

func reveal(show: bool) -> void:
	frame_image.visible = show
	item_image.visible = show

func setup(image: Texture2D, frame: Texture2D) -> void:
	frame_image.texture = frame
	item_image.texture = image

func matches_other_tile(other: MemoryTile) -> bool:
	return other != self and other.item_image.texture == item_image.texture

func kill_on_success() -> void:
	z_index = 1
	var tween: Tween = create_tween()
	tween.set_parallel(true)
	tween.tween_property(self, "disabled", true, 0)
	tween.tween_property(self, "rotation_degrees", 720, .4)
	tween.tween_property(self, "scale", Vector2(1.5, 1.5), .4)
	tween.set_parallel(false)
	tween.tween_interval(0.5)
	tween.tween_property(self, "scale", Vector2.ZERO, 0)
	

func _on_pressed() -> void:
	if Scorer.SelectionEnabled:
		SignalHub.emit_tile_selected(self)
		reveal(true)
	
